// https://rosettacode.org/wiki/FizzBuzz

import { time }

component Main(start) (stop) {
	nodes {
        Range
        Match<int>
        Println
        Unwrap<int>
    }
	net {
		:start -> (
            1   -> range:from,
            101 -> range:to
        )
        range:data    -> unwrap:data
        unwrap:some   -> println:n
        println:n     -> match:data
        100           -> match:case[0]
        match:then[0] -> :stop
    }
}

component Println(n int) (n int) {
    nodes {
        Switch
        Blocker<int>
        Printer<any>
    }
    net {
        :n -> [switch:n, blocker:data]

        switch:fb -> ('FizzBuzz' -> printer:data)
        switch:f  -> ('Fizz' -> printer:data)
        switch:b  -> ('Buzz' -> printer:data)
        switch:else  -> printer:data

        printer:sig -> blocker:sig
        blocker:data -> :n
    }
}

component Switch(n int) (fb int, f int, b int, else int) {
    nodes { Mod }
    net {
        :n -> [
            mod:data,
            (15 -> mod:case[0]),
            (3 -> mod:case[1]),
            (5 -> mod:case[2])
        ]

        mod:then[0] -> :fb
        mod:then[1] -> :f
        mod:then[2] -> :b

        mod:else    -> :else
    }
}